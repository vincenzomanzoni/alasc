options
{
STATIC = false; // default TRUE
LOOKAHEAD = 1; // default 1
IGNORE_CASE = true; // default false
}

PARSER_BEGIN(Parser)
package com.google.code.alasc;

import java.io.*;
import java.util.*;

public class Parser {
	
	private StringBuffer flashListing;
	private TreeSet<Symbol> symbolsTable;
	private ArrayList<Error> errors;
	
	// NOTE: non mettere il costruttore di default, non viene chiamato dall'implementazione di
	// parser.java. Bloody JavaCC!!!
	
		public ArrayList<Error>getErrors()	{	  return errors;	}	public StringBuffer getCode(){	  return flashListing;	}	public TreeSet<Symbol>getSymbolsTable(){	  return symbolsTable;	}
    private String loadTemplateSnippet(String url){
    	FileInputStream fis = null;
		try {
			fis = new FileInputStream(url);
		} catch (FileNotFoundException e) {
			System.out.println("Errore. Non è possibile caricare il file " + url);
		}
    	InputStreamReader isr = new InputStreamReader(fis);
    	BufferedReader myReader = new BufferedReader(isr);
    	
    	StringBuffer snippet = new StringBuffer();
    	String line = null;
    	
    	try {
			line = myReader.readLine();
		} catch (IOException e) {
			System.out.println("Errore. Non è possibile caricare il file " + url);
		}
    	
    	while(line!=null){
    		snippet.append(line);
    		
    		try {
				line = myReader.readLine();
			} catch (IOException e) {
			System.out.println("Errore. Non è possibile caricare il file " + url);
    		}
    	}
    	
    	return snippet.toString();
    }
    
    // TO-DO: far ritornare l'esito della compilazione come da UML
    public void parse(){
    	
    	symbolsTable = new TreeSet<Symbol>();
		errors = new ArrayList<Error>();
		flashListing = new StringBuffer();
    	
    	// Inserisco l'intestazione del template
		flashListing.append(loadTemplateSnippet("head.inc"));
		
		// Genero la personalizzazione del template
		try {
			flashListing.append(start());
		} catch(ParseException pe) {
			System.out.println(pe.toString());
		} catch(SemanticException se) {
			System.out.println(se.toString());
		}
		
		// Inserisco la chiusura del template
		flashListing.append(loadTemplateSnippet("tail.inc"));
    }
    	
	public static void main(String args[]) throws FileNotFoundException {
		Parser parser = new Parser(new FileReader("test-logo.txt"));
		parser.parse();
		
		// TO-DO: Solo per testing
		System.out.println(parser.getCode());
	}
}
PARSER_END(Parser)

SKIP : { " " | "\t" | "\n" | "\r" }

TOKEN :
{
	<NUM: (["0"-"9"])+>
	| <VARNAME: ":"("_"|["A"-"Z"])("_"|["A"-"Z"]|["0"-"9"])*>
	| <FD: "FD">
	| <BK: "BK">
	| <LT: "LT">
	| <RT: "RT">
	| <CLEAN: "CLEAN">
	| <PENDOWN: "PENDOWN">
	| <PENUP: "PENUP">
	| <COLOR: "COLOR">
	| <SUM: "SUM">
	| <DIFFERENCE: "DIFFERENCE">
	| <PRODUCT: "PRODUCT">
	| <QUOTIENT: "QUOTIENT">
	| <MAKE: "MAKE">
	| <LET: "LET">
}

String start() throws SemanticException : {String list;}
{ 
	list = lista()
	{
		return list;
	}
}

String lista() throws SemanticException : {String corr, succ;}
{
	corr = istr() succ = lista()
	{
		return corr + succ;
	}
	|
	{
		return "";
	}
}

String istr() throws SemanticException : {String op;}
{ 
	op = dr_op()
	{
		return op;
	} 
	|
	op = var_op()
	{
		return op;
	}
}

String dr_op() throws SemanticException : {String expr;}
{
	<FD> expr=n_value()
	{
		return "goForward(" + expr + ");";
	}
	|
	<BK> expr=n_value()
	{
		return "goBackward(" + expr + ");";
	}
	|
	<LT> expr=n_value()
	{
		return "rotateLeft(" + expr + ");";
	}
	|
	<RT> expr=n_value()
	{
		return "rotateRight(" + expr + ");";
	}
	|
	<COLOR> expr=n_value()
	{
		return "setColor(" + expr + ");";
	}
	|
	<CLEAN>
	{
		return "clearScreen();";
	}
	|
	<PENDOWN>
	{
		return "penDown();";
	}
	|
	<PENUP>
	{
		return "penUp();";
	}
}

String n_value() throws SemanticException : {String expr;}
{
	expr=n_oper()
	{
		return expr;
	}
	|
	expr=n_s_value()
	{
		return expr;
	}
}

String n_s_value() throws SemanticException : {Token t;}
{
	t=<VARNAME>
	{
		return t.image.substring(1);
	}
	|
	t=<NUM>
	{
		return t.image;
	}
}

String n_oper() throws SemanticException : {String expr1,expr2;}
{
	<SUM> expr1=n_s_value() expr2=n_s_value()
	{
		return expr1 + " + " + expr2;
	}
	|
	<DIFFERENCE> expr1=n_s_value() expr2=n_s_value()
	{
		return expr1 + " - " + expr2;
	}
	|
	<PRODUCT> expr1=n_s_value() expr2=n_s_value()
	{
		return expr1 + " * " + expr2;
	}
	|
	<QUOTIENT> expr1=n_s_value() expr2=n_s_value()
	{
		return expr1 + " / " + expr2;
	}
}

String var_op() throws SemanticException : {Token v,n=null;String expr,varname;}
{
	<MAKE> v=<VARNAME> (n=<NUM>)?
	{
		// TODO: Controllo tabella dei simboli
		varname = v.image.substring(1);
		if (n != null) {
			return "var " + varname + " : Number = " + n.image + ";";
		}
		else {
			return "var " + varname + " : Number;";
		}
	}
	|
	<LET> v=<VARNAME> expr=n_value()
	{
		// TODO: Controllo tabella dei simboli
		varname = v.image.substring(1);
		return varname + " = " + expr + ";";
	}
}
